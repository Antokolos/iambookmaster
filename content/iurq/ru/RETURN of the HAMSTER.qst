:Начало
invkill
perkill
goto Меню

:Меню
pln 
pln   
pln          RETURN  OF  THE  HAMSTER
pln          ВОЗВРАЩЕНИЕ ХОМЯКА СЕМЁНА
pln             Klit ##38$ Korwin
pln          мини-квест на ЛОК 2008
pln 
btn  Перекресток, НАЧАТЬ ИГРУ
btn  Предыстория, Предыстория
btn  От_автора, От авторов
END

:use_Хомяк
pln  Это же ты сам! Опытный котокиллер, матерый взломщик, старый рецидивист хомяк-мутант СемЭн! Мощностью #sila$ х.с.!
end

:use_Пакетик
pln Свернутый в кулек кусок газеты, набитый шелухой от семечек. Вкусно пахнет... Ты не удержался и развернул его!&invkill Пакетик&inv+ Семечка&inv+ Газета
end

:use_Семечка
pln Вкусная семечка! Была... Ты почувствовал себя лучше.&invkill Семечка&sila=sila+1
end

:use_Бумеранг
pln Легкое пластиковое крыло. Легкое для человека. Но не для тебя. Не стоит его таскать с собой повсюду.
end

:use_Газета
pln Изголодавшись по пище духовной, ты читаешь статью на обрывке газеты... "Могли ли себе представить жители города, что в хорошо знакомом им доме, прямо над "Зоомагазином", профессор-генетик Павел Юрьев проводит свои потрясающие эксперименты по выведению нового разумного вида... Станут ли грызуны разумными? - не этот вопрос волнует широкую публику, совсем нет! Имеет ли право профессор Юрьев на подобные опыты? И к каким последствиям они приведут?..." - интересная статья, думаешь ты. Про меня.
end

:use_Башмак
pln Старый китайский башмак. С пористой подошвой - иначе утонул бы уже давно! Но, как известно, производукция заводов Китая - не тонет!!!
end

:use_Резинка
pln Круглая тонкая резинка - в такие люди заматывают пачки денег!
end


:Предыстория
pln Знаменитый своим продолжительным и опасным побегом хомяк Семен выбрался наконец из своей тюрьмы на волю. Теперь ему предстоит решить, куда далее направить свои лапы...
btn Перекресток,НАЧАТЬ ИГРУ
btn От_автора,От автора
btn Меню,Меню
end

:От_автора
pln Авторов у этого квеста двое, так сказать K##38$K. Один из нас придумывает сюжетные ходы, другой критикует и дополняет, потом роли меняются... такой вот творческий союз. Хотим сказать слова благодарности всем нашим друзьям, без которых этот квест не мог бы появиться на свет: DroZ'y и Triangle'у, которые придумали первые игры про Хомяка Семёна, Джону Рональду Руэлу Толкиену, которому принадлежит идея о возвращении главного героя, Джорджу Лукасу (за компанию), и Вам, дорогой игрок - за любезно выделенное Вами на нашу игру время.
btn  Перекресток,НАЧАТЬ ИГРУ
btn  Предыстория,Предыстория
END



:Перекресток
invkill
perkill
sila=1
if not Хомяк then inv+ Хомяк
pln Ты находишься на перекрестке двух дорог. Туда-сюда с бешеной скоростью носятся машины. Горячий от солнца асфальт обжигает лапки. Неподалеку в нём виднеется железная решетка, из-под которой пахнет сыростью и гнилью. Сзади, с тротуара, на тебя с ненавистью глядит огромный бультерьер.
btn Мокро,Подумать
btn Канализация,Прыгнуть в решетку
btn Риск,Бежать через дорогу
btn Тротуар,Вернуться на тротуар
end

:Мокро
pln Ты на проезжей части! Промчавшаяся машина превратила тебя в мокрое пятно на асфальте!
btn Передумать1,Передумать
end

:Передумать1
pln А уже собственно, поздно передумывать... мокрое пятно думать не умеет!
btn Перекресток,Ну это же игра! Отмена! Undo! OOPS! Назад! Пожалуйста!
end

:Тротуар
pln Увернувшись от машины, ты вскочил на бордюр. Радостный бультерьер сразу сунулся мордой к тебе и открыл зубастую пасть. Тебя обдало горячим дыханием.
btn Мокро,Прыгнуть на дорогу
btn Хрум,Прыгнуть в пасть
btn Ням,Шмыгнуть между ног собаки 
end 

:Хрум
pln Ты подпрыгнул вверх и оказался в зубастой пасти. Но за линией зубов, как раз на языке злобного пса. Куда дальше?
btn Ням,Выскочить обратно
btn Уф,Прыгнуть в собачье горло
btn Вау,Укусить язык собаки
btn Ам,Осмотреться
end 

:Уф
pln Раздув щеки, ты прыгнул прямо в дыхательное горло псу! Он закащлялся, захрипел и с трудом выплюнул тебя наружу. Потом понюхал, с отвращением помотал головой и убежал прочь.
btn Газон,Стереть песью слюну об траву
end 

:Вау
pln Вне себя от вони и страха ты вцепился острыми зубками в язык бультерьера! Оказывается, у них всё же есть болевые точки! Обезумевший от боли пёс подскочил в воздух, закащлялся, захрипел и с трудом выплюнул тебя наружу. Потом, подвывая, убежал прочь!
btn Газон,Стереть песью слюну об траву
end 

:Ам
pln Ты осмотрелся. Пасть как пасть - горячая, мокрая, вонючая. Псу надоело, что ты сидишь у него во рту без дела, он мотнул головой, подбрасывая тебя в воздух и перекусил пополам.
btn Порвали,Ой! Назад. Отмена!
end 

:Порвали
pln Что тут отменишь? Ни один хомяк, разорванный пополам, еще не смог после этого натоптать и нагадить еще где-то...
btn Перекресток,Ну это же игра! Отмена! Undo! OOPS! Назад! Пожалуйста!
end

:Ням
pln Ты рванулся изо всех сил! Но разве может хомяк соперничать в реакции с собакой-крысоловом? Страшные челюсти перекусили твое тело пополам.
btn Порвали,Ой! Назад. Отмена!
end 

:Канализация
pln Ты в городском водостоке, проще говоря, в канализации. Сидишь на трубе. Здесь тепло и сыро, - дождь был вчера. Толстые трубы, обмотанные стекловатой и проволокой, ведут направо и налево. Путь влево, кажется, идет чуть-чуть под уклон вниз. Внизу журчит поток. Куда идти?
btn Крысы,Налево, под уклон вниз
btn Кот,Направо
btn Поток,Спрыгнуть с трубы вниз, в поток
end 

:Крысы
pln Ты отбежал от светлого люка совсем недалеко. В темноте ты увидел три пары маленьких красных глаз. Серые крысы - каждая втрое длиннее тебя, с отвратительными голыми длинными хвостами и желтыми зубами! Голодные!!
btn Демагог,Сказать крысам про величие миссии грызунов
btn Скотство,Изо всех сил бежать по трубе обратно
btn Поток,Спрыгнуть с трубы вниз, в поток
end 

:Демагог
pln Не дослушав твою энергичную, страстную речь, они накинулись на тебя и стали делить!!! Вот из-за таких подонков и не могут грызуны достигнуть своего величия, успел подумать ты, раздираемый... скажем, сомнениями в правильности поступка...
btn Порвали,Ой! Я лучше назад полезу!
end 

:Скотство
pln Ты бросился бежать! Крысы следом! Вы пронеслись через освещенный участок, в темноте ты прыгал через опутывающую трубу проволоки, крысы, молча прыгали следом, и вдруг, вся ваша кавалькада, выбежала навстречу огромному, драному бродячему коту! Котяра, слегка напуганный ТАКИМ количеством съестного, опешил и встал в боевую стойку. Крысы на миг тоже замерли, поводя в воздухе усиками и пытаясь решить, что делать... пора решить этот вопрос и тебе!
btn Норка,Притаиться, забиться в обмотку трубы и сидеть тихо
btn Поток,Спрыгнуть с трубы вниз, в теплую темноту
end
 
:Норка
pln Ты находишь отставший клочок стекловаты и быстро лезешь под него. Шкуру колют стеклянные ворсинки. Сверху раздается дикий кошачий мяв и крысиный писк. Наконец, все стихло...
btn Мясо,Высунуть мордочку и осмотреться
btn Соник,Посидеть еще немного
end 

:Мясо
pln  Ты высовываешь мордочку и обнаруживаешь, что котяра преспокойно жрёт самую толстую крысу. Двух других не видно - наверное, сбежали. А вот если бы они объединились, могли бы и кота слопать! - думаешь ты грустно... 
btn Соник,Посидеть еще немного
btn Добыча,Прорваться, пока кот отвлекся на крысу
end

:Туннель
pln Труба по которой ты бежишь, вдруг расширяется и уходит вверх, в потолок! В том месте, куда она ведет есть узкая щель. С другой стороны, можно всегда спрыгнуть вниз, в кислый вонючий поток. И еще есть возможность - вернуться назад 
btn Туалет,Наверх
btn Наверх,Назад
end

:Соник
pln Посидев в колючей норке еще немного, ты обнаружил, что кот наелся и улегся спать сверху... И как теперь быть? Есть хочется страшно, шкура исколота стекловатой, а сверху сидит это наглое животное и счастливо храпит во сне!
btn Самоубийца,Укусить кота!
btn Добыча,Тихонько проползти мимо наглых усов
btn Шуба,Обмотаться стекловатой
end 

:Самоубийца
pln Ты выскочил из норки и отважно укусил кота за ухо! От дикого рева можно было оглохнуть! Но, напуганный бродячий котяра отмахнулся от тебя когтистой лапой! Столкновение с когтями плохо отразилось на состоянии твоего пузичка...
btn Порвали,Ой! Я же должен быть круче!! Мы так не договаривались! Отмена!!
end 

:Добыча
pln Ты выскочил из норки и тихонечко потопал мимо кота... Кот тихонечко шевельнулся и тихонечко накрыл тебя лапой. Он долго играл с тобой в кошки-мышки...
btn Отсидка,Ой! Я лучше бы в норке посидел!
end

:Отсидка
pln Знамо дело, лучше!
btn Канализация,Отмена! Undo! OOPS! Назад! RESET!
end

:Шуба
pln Ты обмотался стекловатой, как шубой и вылез из норки. Ошалевший кот увидел ТАКОГО ТОЛСТОГО ХОМЯКА, что наверное, прослезился от счастья! Он схватил тебя лапами с боков и укусил... твою колючую шубу! МЯУУУУУ! - кота как ветром сдуло! Фыркая, ругаясь и отплевываясь, он и думать о тебе забыл, убегая по трубе туда, где раньше сидели крысы...
btn Поток,Спрыгнуть с трубы вниз
btn Почиститься,Идти дальше по трубе
end 

:Почиститься
pln Сбросив с себя ошметки стекловаты - очень уж колко! - ты устремился вперед.
btn Наверх,Бежать без оглядки
btn Наверх,Бежать с оглядкой
end

:Наверх
pln Вскоре ты добежал до места, где можно было выбраться наверх. Над головой - канализационный люк с отколотым краем. Впрочем, можно бежать по туннелю и дальше. А можно спрыгнуть вниз! Свобода!
btn Тротуар2,Вылезти через дырку наверх
btn Туннель,Бежать дальше
btn Кот,Вернуться в компанию кота
btn Пузыри,Спрыгнуть в вонючий поток
end 

:Кот
pln Долго ты бежал по темной трубе, слушая журчание воды под ногами. Наконец, ты уперся во что-то большое, мохнатое, страшное. Да это же БРОДЯЧИЙ КОТ!!! Обрадованный встречей, котяра тут же наполнил свой тощий живот.
btn Скотина,Ой! Я лучше назад полезу!
end 


:Скотина
pln Что? Ни один хомяк, Знай, мой юный... хомяк, что ни один грызун, переваренный котом, не вернулся к Великой Силе в прежнем облике!
btn Канализация,Ну это же игра! Ну пожалуйста!
end

:Поток
pln Упав в вонючую жидкость - ты предпочитал думать о ней, как об очень грязной воде, - ты стал захлебываться. В кромешной темноте, среди сильных запахов плесени, химии и отходов жизнедеятельности, ты потерял ориентировку. Что делать?
btn Пузыри,Пускать пузыри
btn Пузыри,Нырнуть
btn Башмак,Барахтаться, как можно дольше
end 

:Пузыри
pln Погрузившись в поток, ты стал пускать пузыри: буль-буль-буль. Воздуха хватило ненадолго.
btn Русал,Хочу наверх!
end


:Русал
pln Многие хотели! Да вышло вот у немногих!
btn Канализация,Ну это же игра! Ну пожалуйста!
end

:Башмак
inv+ Башмак
pln Тебе повезло - ты выбрался на плывущий по течению старый башмак. Мокрый, грязный, но не потерявший веры в свое счастливое зернышко, ты сидишь на качающемся башмаке, который плывет в полной темноте по канализационной трубе. Поток, тем временем, начинает убыстряться, впереди что-то шумит...
btn Слив,Вцепиться в башмак
btn Водоворот,Спрыгнуть в "воду" и плыть к берегу
end 

:Водоворот
invkill Башмак
pln Ты спрыгнул с башмака и стал отчаянно работать лапками... но куда тут плыть? Здесь совершенно темно! От вони твой мокрый носик не может ничего разобрать! Шерстка совсем намокла, и ты почувствовал, как водоворот закружил тебя и потянул ко дну...
btn Вынырнуть,Хочу наверх!
end 

:Вынырнуть
pln Многие хотели! Да вышло вот у немногих!
btn Канализация,Ну это же игра! Ну пожалуйста!
end

:Слив
pln Водоворот закружил старый башмак, ударяя о стены трубы, но ты вцепился в него когтями и зубами просто насмерть! Башмак давно наполнился водой, но пористая подошва не давала ему окончательно утонуть. Вдруг шум стал оглушительным, и ты почувствовал, как летишь куда-то вместе с башмаком!
btn Камни,Разжать зубы
btn Река,Забиться внутрь башмака
end 

:Камни
invkill Башмак
pln Ты разжал зубы и оторвался от своего намокшего кораблика. Падающий из трубы поток ударил тебя о камни и затянул вглубь...
btn Вынырнуть2,Вынырнуть!
end 
:Вынырнуть2
pln Многие хотели бы! Но после удара башкой о камни, эта умная мысль пришла тебе в голову слишком поздно!
btn Канализация,Ну это же игра! Ну, пожалуйста!
end

:Река
pln Удержавшись за старый китайский башмак, ты спас голову от ударов о камни, и всплыл вместе с ним на поверхность реки. Вскоре вода стала чистой, а река - широкой. Солнышко слегка обсушило твою шкурку. Ты согрелся, но почувствовал голод. Над рекой послышались крики чаек. Ты видишь, что башмак проплывает неподалеку от песчаной косы - прикидываешь расстояние, - доплыть вполне можно. За косой виднеется зеленый берег, поросший травой.
btn Берег,Спрыгнуть в воду и плыть к берегу
btn Чайки,Сидеть на своем кораблике и греться на солнышке
end


:Чайки
pln Ты почти совсем обсох, твой мех снова стал ярким. Привлеченные светлым пятном зоркие чайки не пропустили добычу, - ты не успел ничего сообразить, как острый клюв пробил твою голову...
btn Грамотей,А разве чайки едят хомяков?! Они должны есть рыбу!
end 

:Грамотей
pln Это ты тут грамотный! А чайки - не очень грамотные, даром что городские и на помойках побираются! Хотя, с другой стороны, твои мозги - одобрили! В общем, съели они тебя, несмотря на твою грамотность...
btn Река,Эй, тогда отмена! Птицы!! Вам говорю! Покладите мозг на место!
end

:Берег
invkill Башмак
pln Выбравшись на берег, ты вскоре нашел зеленый луг. А на лугу - норку. А в норке - пушистую, толстую красивую девочку. Хомку. И стали вы с ней жить-поживать, да зерно воровать. И вскоре нарожали дюжину очаровательных, умненьких хомячишек...
btn конец2,- А дальше?
end 

:конец2
pln  
pln ХОМЯК СЕМЁН ВЕРНУЛСЯ К ПРИРОДЕ! И СТАЛ ПРЕУСПЕВАЮЩИМ ХОМЯКОМ-ПРОИЗВОДИТЕЛЕМ.
pln  
pln                                 KOHEЦ
pln
pln Это одна из трех концовок истории про возвращение хомяка Семёна. Не самая лучшая для ВЕЛИКОГО ПРОГРЕССА ЦИВИЛИЗАЦИИ ГРЫЗУНОВ, но вполне счастливая для хомяка Семёна лично и самая жизнеутверждающая. 
btn Заново,А другой вариант окончания этой истории есть?
end 

:Риск
if rnd4=2 then pln Ты увернулся от пары колес, побежал дальше семеня ножками... но удача не может сопутствовать тебе, - да и кому бы то ни было, - вечно. &goto Мокро else pln Рванувшись бежать через дорогу, ты едва увернулся от пронесшихся мимо колес! Асфальт дрожит под ногами, от гула приближающихся машин. В данную секунду ты добежал до прерывистой белой полосы и замер на ней. Что делать дальше?
btn Риск2,Бежать вперед, на другую сторону дороги!
btn Решетка,Прыгнуть в решетку канализации!
btn Обгон,Бежать вдоль дороги по белой полосе!
end 


:Обгон
if rnd2=1 then Не повезло! Одна из машин пошла на обгон! &goto Мокро else pln Ты еле увернулся от машины, пошедшей на обгон и задумался - а что тебе собственно дает этот бег по белой полосе? Судя по всему, она очень длинная и ни на шаг не приближает тебя к спасительному тротуару...
btn Риск2,Бежать вперед, на другую сторону дороги!
btn Обгон,Бежать дальше вдоль дороги по белой полосе!
end 

:Решетка
pln Ты решил, что здесь слишком опасно и прыгнул в щель между прутьями!#$
goto Канализация
end

:Риск2
if rnd4=2 then pln Ты увернулся от машины, побежал дальше, к спасительному тротуару на другой стороне, быстро семеня ножками... но удача не может сопутствовать тебе, - да и кому бы то ни было, - вечно. &goto Мокро
pln Рванувшись бежать через дорогу, ты едва увернулся от пронесшихся мимо колес! Асфальт дрожит под ногами, от гула приближающихся машин. Но, к счастью, все уже позади! Ты выбираешься на тротуар на другой стороне дороги. Что делать дальше?
goto Тротуар2
end 

:Тротуар2
musor=0
if trot=0 then p Ты наконец в более-менее безопасном месте. &trot=1
pln На тротуаре. В цветочной клумбе. Здесь можно отдышаться и осмотреться, чтобы решить куда двигаться дальше. Рядом с клумбой находится скамейка и мусорная урна. По тротуару ходят люди.
btn осм_цветы,Осмотреть цветы
btn осм_скам,Осмотреть скамейку
btn осм_народ,Осмотреть людей
btn Осм_урну,Осмотреть мусорную урну у клумбы
end 

:осм_цветы
pln Цветы как цветы: желто-оранжевые, пахучие. Невкусные.
btn Тротуар2,Пожевать что-нибудь другое
end

:осм_скам
pln Скамейка покрашена. Поэтому пахнет очень неприятно. И, возможно, поэтому, на ней никто не сидит.
btn Тротуар2,Понюхать что-нибудь другое, более съедобное
end

:осм_народ
pln Люди - такие беспокойные существа... Одни идут налево, другие направо. Неужели им непонятно, что бессмысленно идти туда, откуда уже идут: ведь там, наверное, уже все съели!
btn Тротуар2,Что-то все мысли о еде... давно надо подкрепиться...
end

:Газон
pln Итак, ты на коротко подстриженном газоне. Вокруг играют дети: раздается смех, радостные крики, удары мяча. С чистого неба светит яркое солнце.
btn скрыться,Спрятаться в траве
btn нафиг_нафиг,Бежать через дорогу
btn суслик,Встать на задние лапки и осмотреться
end 

:скрыться
pln Ты попробовал спрятаться. Но трава была слишком коротка. Твою светлую пушистую спинку прекрасно видно на ярко-зеленом фоне!
btn газон,Осмотреться вокруг
end

:нафиг_нафиг
pln Снова на дорогу с этими ужасными машинами? Ты там уже был! И второй раз тебя туда не заманишь!
btn газон,Осмотреться вокруг
end

:суслик
pln Почти сразу тебя заметила маленькая девочка: "Мама, мама, смотри какой пушистик!" Она без опаски хватает тебя и поднимает вверх. Ее родительница приближается.
btn Диалог,Сидеть паинькой и делать вид, что тебе это нравится
btn Сапоги,Укусить девчонку за палец
btn Шмяк,Спрыгнуть с рук на газон
end

:Шмяк
pln Ты ловко спрыгиваешь обратно на траву.
pln - Ян, - кричит девочка, - поймай мне пушистика!
pln Немедленно появляется подросток, который ловко нападает на тебя сверху. Трудно убежать короткими лапками по стриженному газону от длинноногого человека... Через несколько секунд ты так плотно зажат у него в кулаке, что дышать становится совершенно нечем!
btn Клетка,Смириться со своей судьбой и не трепыхаться.
end

:Диалог
pln - И вправду, симпатичный хомячок! Где ты его взяла? - спрашивает мама.
pln - Здесь. Он, наверное, потерялся! Мама, можно я его возьму себе?
pln - Ну, только если у него не найдется хозяин!
pln - Спасибо, мама, ты самая лучшая! - сказала девочка и предложила тебе пожевать печенье.
btn Клетка,Слопать печенье
btn Сапоги,Укусить девчонку за палец
end 

:Сапоги
pln - Ой! - кричит девочка.
pln - Ай! Мерзкое животное! - кричит ее мама. - Ян! Сделай что-нибудь!
pln Мальчишка постарше - видимо, брат закусанной девочки, начинает топать ногами и лупить палкой по тебе. А на широком газоне бежать совершенно некуда. Опустим занавес на этой печальной сцене... 
btn зверство,Ой, я тогда не буду кусаться! Я передумал!
end

:зверство
pln А уже собственно, поздно передумывать... мокрое пятно думать не умеет!
btn Перекресток,Ну это же игра! Отмена! Undo! OOPS! Назад! Пожалуйста!
end

:Клетка
pln ... Через час ты снова сидел в клетке.  Рядом была кормушка - почти такая же, как и раньше. Беговое колесико - крутится еще лучше прежнего. Поилка - правда с водой, а не с прокисшим молоком. И кота здесь, кажется, нет... Ты грыз вкусное зерно и размышлял о поворотах судьбы: Неужели вся наша жизнь, это побег из одной клетки в другую?
btn конец1,- А дальше?
end 

:конец1
pln  
pln        ХОМЯК ВЕРНУЛСЯ В КЛЕТКУ!
pln  
pln                 KOHEЦ
pln
pln Это одна из трех концовок истории про возвращение хомяка Семёна. Не самая лучшая, но самая философская. 
btn Заново,А другой вариант окончания этой истории есть?
end

:Заново
pln Разумеется, есть!
btn Перекресток,Тогда сначала
end
;___________________________________________


:Осм_урну
p Ты сидишь под остро пахнущей скамейкой около урны. 
if (musor=0) then pln Здесь ты нашел вкусно пахнущий пакетик с шелухой от семечек.&inv+ Пакетик&musor=1
btn Урна,Залезть в урну
btn Скамейка,Залезть на скамейку
end


:Скамейка
pln Ты забираешься на липкую от краски скамейку. Голова кружится, лапы прилипли к дереву! Тебе плохо. Мимо бегут мальчишки. Заметив тебя, они смеются и тыкают в тебя палками. А ты элементарно не можешь сдвинуться с места! Потом один говорит: "Он наверное больной, заразный! Давайте его убъем!" И они делают это... Как жесток этот мир!
btn die_bench,Я не хотел на скамейку! Я передумал!
end


:die_bench
pln А уже собственно, поздно передумывать... дохлые хомяки занимают в рейтинге IQ место между обломками кирпичей и дохлыми тараканами. Сразу после последних.
btn Тротуар2,Ну это же игра! Отмена! Undo! OOPS! Назад! Пожалуйста!
end 

:Урна
pln Когда ты залез на край урны, тебе резко расхотелось залезать внутрь - оттуда пахло плесенью, гарью и остатками краски. Зато сверху было отлично видно всю улицу. Прямо перед тобой - магазин "Игрушки", справа от него - "Зоомагазин", слева - продуктовая лавка.
btn Игрушки,В магазин "Игрушки"
btn Зоомагазин,В "Зоомагазин"
btn Продукты,В магазин "Продукты"
end

:Улица
pln Ты снова на улице. Куда бежим теперь?
btn Игрушки,В магазин "Игрушки"
btn Зоомагазин,В "Зоомагазин"
btn Продукты,В магазин "Продукты"
end 

:Игрушки
pln Ты прошмыгнул в магазин игрушек. Здесь игрушек. И много детей!
btn Зевака,Осмотреться
btn Щелка,Забежать под полки
end 

:Щелка
p Ты забился в щель под полками и дождался наступления вечера. Маленькие и большие люди ушли.#$
goto Игрушки2
end 

:Игрушки2
pln Ты стал спокойно рассматривать и обнюхивать магазин. Тут у нас машинки, тут у нас мягкие игрушки, тут погремушки для лялек... Вот какие-то мячики, шарики, бумеранги, скакалки. А вот открытое окно. А напротив другое, тоже открытое. И оттуда доносится до тебя знакомый с детства звук - мерное гудение лабораторной вентиляции. Ты вспомнил! Это - твой дом! Тебе надо попасть именно туда! Только как туда добраться?
if not Бумеранг then btn Взять_бумеранг,Взять бумеранг
if not Резинка then btn Подобрать_резинку,Подобрать резинку
btn Ракета,Осмотреть ракетную установку 
btn Улица,На улицу
end 

:Взять_бумеранг
inv+ Бумеранг
p Ты взял тяжеленный и громоздкий деревянный бумеранг, оклеенный фольгой.
btn Игрушки2,Дальше
end

:Подобрать_резинку
inv+ Резинка
p Ты взял тонкую кольцевую резинку. Она невкусно пахла деньгами, потом и пылью.
btn Игрушки2,Дальше
end

:Ракета
p На полке стоит прикольная модель ракетной установки с пружинным приводом... 
if navel=1 then p  Ракета наведена в окно лаборатории. 
if navel=0 then btn наводчик,Навести ракету на окно лаборатории
if spin=1 then p  Пружина взведена. else btn пружинка,Взвести пружину
btn Страховка,Залезть на ракету
btn Игрушки2,Отойти от ракетницы
end 

:пружинка
if sila>2 then spin=1&p  Пружина взведена. else p  Не хватает сил взвести пружину!
btn ракета,Осмотреть ракету
end

:страховка
if Резинка then invkill Резинка&p  Ты привязался резинкой к пластмассовой ракете.&btn Полет,Нажать на спуск else p  Нечем привязаться&btn ракета,Осмотреть ракету
end

:Наводчик
navel=1
pln Сделано! Ракетная установка смотрит в окно лаборатории!
btn ракета,Осмотреть ракету
end


:Полет
if navel=1 then pln Ты лапкой дотягиваешься до спусковой кнопки - немыслимый акробатический этюд, между прочим, жаль профессор не видит! - и установка срабатывает, вышвыривая ракету и тебя в окно!&goto Планирование else pln Ракета ударила тебя об стенку магазина!&goto Шмякc           
end

:Планирование
if Бумеранг then pln Ракета летит по баллистической кривой, которая должна бы упереться ПОД подоконник, но игрушечный бумеранг начинает вращаться и, как крыло, поднимает и ракету и тебя в окно! С грохотом, совершенно ошеломленный от вращения, ты приземляешься на подоконник лаборатории!&invkill&inv+ Хомяк else pln Ракета летит по баллистической кривой и, разумеется с силой бьет тебя головой в каменную стенку под подоконником!&goto Шмякc2
btn Лаборатория,Отвязаться от бумеранга
end 

:Шмякc
pln  Шмяк! Ты размазан по стене!
btn склероз,Ой! Забыл навести!
end 

:Шмякc2
pln  Шмяк! Ты размазан по стене!
btn склероз,Ой! Совсем про баллистику забыл! Или не знал? Неважно. Важно, что надо было какую-нибудь штукенцию для полета не забыть взять!
end 

:склероз
pln Способ, которым тебе об этом напомнили, не оставляет в жизни шансов на исправление ошибки!
btn Игрушки2,OOPS!
end

:Зевака
pln Ты встал столбиком и осмотрелся: тут у нас машинки, тут у нас мягкие игрушки, тут погремушки для лялек... и вдруг тебя обхватила детская ладошка.
pln - Мама, смотри какая живая игрушка! Хочу! Ой, она царапкается! Смешная!
pln - Где ты это взяла? Дай сюда немедленно! Он, наверное, из зоомагазина сбежал! Давай туда отнесем!
pln - Мамочка, купи мне его на день рождения!
pln - Ты серьезно? Ну хорошо, тогда купим ему еще и клеточку покрасивее. Только ухаживать будешь сама!
pln - Мама, ты лучше всех!
btn Клетка,Покориться судьбе
end 

:Зоомагазин
pln Ты прошмыгнул в зоомагазин. Здесь много детей! И много авкариумов с рыбками и клеток с грызунами и черепахами. И есть важный кот, спящий на специальной пушистой подставочке!
if kor=1 and Бумеранг then btn Котя,В щель под внутренней дверью!
if kor=1 and not Бумеранг then btn Коридор,В щель под внутренней дверью!
btn Улица,На улицу
btn Зевака2,Осмотреться
end 

:Котя
pln Ты попробовал шшмыгнуть под дверь... не тут-то было! Дурацкий Бумеранг застрял под дверью, и тебя догнал Важный Кот... Нельзя сказать, чтобы он не любил хомяков - напротив, весьма любил. И, как это ни прискорбно, его любовь пагубно отразилась на блестящих перспективах хомячьего рода стать разумными!
btn Улица,Хватит издеваться! Забыл я про этот Бумеранг! Назад!
end

:Зевака2
pln Ты встал столбиком и осмотрелся: тут у нас птички, тут рыбки, тут черепашки, тут проснувшийся КОТ!!!! И вдруг тебя обхватила детская ладошка.
pln - Мама, смотри какая живая игрушка! Хочу! Ой, она царапкается! Смешная!
pln - Где ты это взяла? Дай сюда немедленно! Он, наверное, из клетки сбежал! Давай отдадим тете продавцу!
pln - Мамочка, купи мне его на день рождения!
pln - Ты серьезно? Ну хорошо, тогда купим ему еще и клеточку покрасивее. Только ухаживать будешь сама!
pln - Мама, ты лучше всех!
pln Кот провожает тебя равнодушным взглядом.
btn Клетка,Покориться судьбе
end 

:Продукты
pln Ты забежал в магазин "Продукты". И понял, что чудо свершилось! Ты достиг рая! Здесь были и чипсы, и мука, и сыр, и крупы, и даже свежие овощи! Здесь думали про хомяков! Даже люди бывают приятными. Иногда. Вот только их тут очень много. Надо дождаться ночи. Ты забился под продуктовые полки и обнаружил, что здесь есть все для счастья: бумага для постройки гнезда, мусор и кучка зерна для хомяков!
btn Отрава,Подкрепиться зерном
btn Ловушка,Построить гнездо из бумаги
if sila<2 then btn Кулек,Стащить кулек сухариков с полки
btn Улица,На улицу
end 

:Отрава
pln Ты набрал зерна в защечные мешки. Кое-что и проглотил - было вкусно. Через несколько часов ты почувствовал страшную резь в животе и понял, как люди коварны!
btn Кранты_1,Выплюнуть эту гадость!!
end 

:Ловушка
pln Ты начал строить гнездо. И намертво прилип к бумажке, смазанной противным клеем. Она никак не хотела отрываться... \n- И зачем мне нужно было это гнездо, старому холостяку и бродяге! - с тоской думал ты, когда уборщица завернула тебя в ту же бумажку и выбросила в мусорный бак.
btn Кранты_2,Выбраться!!
end 

:кранты_1
pln А вот уже поздно!
btn Продукты,Отмена!!
end

:кранты_2
pln Бумажка прилипла насмерть! Вот такой печальный каламбур.
btn Продукты,Отмена!!
end

:Кулек
pln Ты схватил зубами пакетик сухариков и потихоньку уволок в уголок. Там, в укромной тени, ты наконец-то смог подкрепиться. Даже сил прибавилось! Только во рту стало очень солёно. Чего бы попить?
sila=sila+1
btn Улица,Выйти из магазина
btn Ловушка,Построить в магазине гнездо из бумаги
btn Молоко,Прогрызть пакет молока
end 

:Молоко
pln Ты схватил зубами пакет с молоком и быстро отгрыз уголок. Молоко тонкой струйкой потекло на пол. Этого хватило, чтобы утолить жажду. Лужа на полу растет и скоро подберется к кучке зерна, рассыпанного под полкой.
sila=sila+1
btn Ловушка,Построить в магазине гнездо из бумаги
btn Отрава,Запастись зерном
btn Улица,Выйти из магазина
end 

:Туалет
pln Ты в старом грязном туалете. Только дыры в полу не позволяют лужам распространиться на всю его поверхность. Люди - существа, безусловно, вонючие и отвратительные. Не в пример, нам, хомякам.
btn Коридор,Выбежать наружу
btn тубумага,Спрятаться в туалетную бумагу
end 

:тубумага
pln Замечательная идея! Только ЗДЕСЬ НЕТ ТУАЛЕТНОЙ БУМАГИ!
goto Туалет

:Коридор
kor=1
pln Ты в полутемном коридоре. С одной стороны - закрытая дверь, с довольно широкой щелью внизу. С другой - лестница, ведущая вверх. Из-под двери пахнет птичьим кормом и пометом. Сверху пахнет чем-то знакомым. Куда пойти?
btn Зоомагазин,В щель под дверью
btn Лестница,Вверх по лестнице
end

:Лестница
pln Ты наверху лестничной площадки. Перед тобой тяжелая дверь. Она почти закрыта.
if sila>2 then btn Лаборатория,Открыть дверь носом  else btn Слабость,Открыть дверь носом
btn Коридор,Вниз
end 

:Слабость
pln Тебе не хватает сил. Наверное, от голода. А носу больно...
btn Лестница,Потереть нос
end

:Лаборатория
num1=3
num2=0
pln Ты - действительно суперский хомяк! И ты снова в лаборатории! Родные стены, родной, знакомый запах ферментов, мерное гудение центрифуги, пощелкивание счетчика над обучающим лабиринтом... И твоя родная клетка, откуда ты сбежал год назад и глупо попался в зоомагазине в лапы прежней хозяйке... Теперь тебе не хочется отсюда бежать. Ты знаешь свою цель - это развитие. Это - Познание. Это - размножение нового вида хомяков... А кстати, в твоей бывшей клетке сидит оч-ч-чень симпатичная особа. Пушистая. Одинокая. С умными глазами. Надо вспомнить, как там открывается кодовый замок на клетке? 
btn замок,Подойти к клетке
end 

:замок
pln Ты стоишь у клетки. За решетчатой дверцей на тебя глядят два быстрых внимательных глаза. Замок другой, не тот, что был здесь когда-то - на хитром механизме видны два вращающихся колесика с цифрами от 0 до 3. Напротив каждого колесика на механизме есть риска.
pln Первое колесико установлено цифрой #num1$ против риски, второе установлено цифрой #num2$.
btn колесо1,Повернуть первое колесико
btn колесо2,Повернуть второе колесико
btn вклетке,Дернуть замок
btn наклетке,Залезть на клетку
end  

:наклетке
pln По решетке ты залез на клетку и сверху увидел пластиковый квадратик, на котором краской был нарисован знакомый знак, который в опытах профессора обозначал пять кусочков сыра или пять зернышек или пять хомяков.
btn замок,Слезть с клетки
end

:колесо1
num1=num1+1
if num1=4 then num1=0
pln Ты повернул левое колесико на одно деление.
btn замок,Посмотреть что получилось
end

:колесо2
num2=num2+1
if num2=4 then num2=0
pln Ты повернул правое колесико на одно деление.
btn замок,Посмотреть что получилось
end

:вклетке
if num1+num2<>5 then p Ничего не вышло. Наверное, код не тот! &goto замок
pln Замок щелкнул и открылся. Дверца распахнулась. Ты замер на миг в нерешительности на пороге - очень уж не хотелось попадать снова в клетку. Но в этот момент тебя накрыла человеческая рука и затолкнула внутрь. Ты услышал скрежет замка и человеческий голос. Голос профессора Юрьева, так хорошо тебе знакомый:
pln - Это чудо. Семен вернулся. Вернулся наш гениальный хомяк! Теперь дело моей жизни не погибнет! Теперь у людей будут разумные партнеры на этой планете... Слышишь, Семен? Ты должен исполнить свой долг перед видом и стать отцом новой расы! Смотри как на тебя смотрит Мира! Не робей и не сомневайся, это самая умная хомячиха из всех, что я знаю!
pln Мира подошла к тебе, обнюхала и отбежала в сторону, заигрывая...
btn конец3,Исполнить долг перед видом
end

:конец3
pln  
pln ХОМЯК ВЕРНУЛСЯ ДОМОЙ, В ЛАБОРАТОРИЮ...
pln ОТСЮДА НАЧНЕТСЯ ПРОГРЕСС ЦИВИЛИЗАЦИИ ГРЫЗУНОВ
pln И ТОГДА ЛЮДИ ДОЛЖНЫ БУДУТ ПОТЕСНИТЬСЯ!
pln НО ЭТО - УЖЕ СОВСЕМ ДРУГАЯ ИСТОРИЯ...
pln  
pln                 KOHEЦ
pln
pln Это одна из трех концовок истории про возвращение хомяка Семёна. Не самая лучшая ДЛЯ ЧЕЛОВЕЧЕСТВА, но оно же всегда мечтало о встрече с братьями по разуму? 
btn Заново,А другой вариант окончания этой истории есть?
end 
