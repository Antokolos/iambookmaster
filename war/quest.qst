:1
pln "Древний кинжал"
pln
pln ver. 0.8
pln
pln Автор: Kaschey (textquest@nm.ru)
p http://textquest.nm.ru
if Древний кинжал then cls & pln & pln & pln & p -={ Для повторной игры загрузите квест заново (Ctrl+Q) }=-
btn 6,Начать квест
btn 2,Вступление
btn 3,Обучение
btn 5,Реклама
btn 4,Авторы
btn Exit,Выход
end

:2
pln Много лет минуло с тех пор, как вернулся король. Мир и спокойствие пришли в Средиземье. Люди, эльфы, гномы, хоббиты и другие свободные народы уже почти забыли те времена, когда они боролись с Врагом, в великой войне. Войне Кольца.
pln За победу пришлось заплатить тогда немалую цену. Многие великие воины полегли на поле брани, но Саурон и его приспешники были разгромлены. Девять кольценосных призраков были навсегда изгнаны из мира живых.
pln Но согласно древнему пророчеству, настанет день, когда древняя реликвия возродится снова, и тогда над Мордором вновь сгустится тьма.
pln
pln Вы - хоббит Фред, прямой потомок самого великого Фродо, сына Дрого, прямой наследник Бильбо. Вы живете в вашем родовом поместье под названием Бебень на Бугре, которое расположено на севере Удела. Там-то все и началось....
btn 1,Вернуться
end

:Exit
pln Спасибо за игру!
pln
p Нажмите CTR+X чтобы выйти из игры.
end

:3
pln Всё очень просто. Вы выбираете комманду посредством нажатия на кнопку. В игре вы сможете разговаривать с персонажами, находить и использовать предметы, торговать, исследовать таинственные места...
pln Вот несколько советов по квесту:
pln
pln 1. Внимательно читайте описания локаций - там содержится много полезной информации.
pln
pln 2. Не торопитесь нажимать на кнопки - это ни к чему хорошему не приведет.
pln
pln 3. Возвращайтесь в игровые локации несколько раз - там могут появиться новые возможности.
pln
pln 4. Найдя очередной предмет подумайте как бы вы сами его использовали.
pln
pln 5. Не сохраняйтя игру слишком часто - может не хватить денег, необходимых для окончания квеста.
pln
p 6. Все неточности, ошибки, вопросы, замечания, предложения, советы и т.д присылайте на e-mail: textquest@nm.ru
btn 1,Вернуться
end

:4
pln Данный квест написан под впечатлением от прочитанной трилогии Джона Р.Р. Толкина "Властелин колец". Настоятельно рекомендую вам прочитать данное произведение перед началом игры.
pln
pln Автор:			Кащей
pln Идея:			Кащей
pln Программист:		RipOs
p Бета-тестеры:		Лена, Ант, HellFire, JohnB, Devil
btn 1,Вернуться
end

:5
pln Если вы хотите разместить свою рекламу в моем квесте или на одном из моих сайтов, пишите:
pln textquest@nm.ru
pln
pln Интересные сайты:
pln
pln 1. Текстовый Квест (http://textquest.nm.ru)
pln Сайт посвященный квесту "Древний кинжал" и другим текстовым квестам на русском языке.
pln
pln 2. Замок Кащея (http://kaschey.newmail.ru)
p Всё о заработке в сети, халява, галерея эротики, бесплатные лотереи и игры с призами, софт и др.
btn 1,Вернуться
end

:6
perkill
pln Уже который месяц ты хотел отремонтировать свое родовое поместье, но, как часто это бывает у хоббитов, то времени мало, то...есть хочется.
pln Но вот, наконец-то тебе представилась удобная возможность начать ремонт сегодня.
btn 19,Выйти из дома
btn 7,Начать ремонт
btn 8,Перекусить
btn 9,Покурить трубку
btn 10,Сходить в гости
end

:7
pln Так много всего предстоит сделать. С чего же начать?
btn 19,Выйти из дома
btn 11,Вымыть потолок
btn 12,Переставить мебель
btn 13,Починить старую печь
btn 14,Забить дырку в полу
btn 15,Покрасить стены
end

:8
pln Ты приносишь из кладовой вместительную корзину с фруктами, сыром, колбасой, сушеной рыбой, свежими лепешками, бутылочкой гондорского вина... И чего там только нет - не зря же ты ходил сегодня на рынок.
p Разложив все это на столе ты начинаешь неспеша обедать. Да, ремонт пожалуй, придется отложить.
btn 6,Вернуться
end

:9
p Подойдя к окну ты раскуриваешь свою деревянную трубку. "Долгодольский лист" самый лучший табака на всем севере. Наверное именно поэтому ты расслабляешься, и тебя начинает клонить в сон.
btn 6,Вернуться
end

:10
p Хотя Беббинсы никогда не нравились тебе, их, как близких родственников, приходится иногда навещать. Ты уходишь к ним в гости, потом заглядываешь к Тукам, затем к Брендибакам.... Вернувшись на закате, ты укладываешься спать, напрочь позабыв о том, что хотел начать делать ремонт.
btn 6,Вернуться
end

:11
p Потолки в усадьбе невысокие, впрочем, как и сами хоббиты. Поэтому тебе без труда удается вымыть потолки в каждой комнате.
btn 7,Вернуться
end

:12
p Под отодвинутым в сторону комодом, ты находишь коробок спичек.
btn 7,Вернуться
if not Коробок спичек and not Записка and not Шкатулка and not Старая монета and not Дорожный плащ and not Факел and not Принести масло then btn 12a,Взять спички
btn 12b,Зажечь спичку
end

:12a
p Ты взял коробок спичек.
inv+ Коробок спичек & cls
goto 7

:12b
pln Ты зачарованно смотришь на ярко горящую спичку, но когда она обжигает твои пальцы, ты роняешь ее на пол. Она летит вниз и проваливается в щель в полу. Деревянный пол быстро загорается.
p После того, как твои друзья всё же потушили Бебень, они нашли лишь обгоревший скелет маленького хоббита.
end

:13
p Для того чтобы починить печь необходимы специальные инструменты. Насколько ты помнишь у тебя таких никогда не было.
btn 7,Вернуться
if Коробок спичек then btn 13a, Растопить печь
if Доски then btn 13b, Бросить доски в печь
end

:13a
pln Ты бросаешь в сломанную печь несколько дубовых поленьев, а потом чиркаешь спичкой и поджигаешь их.
pln Дрова быстро разгораются, но вся комната так же быстро заполняется едким дымом. Ты бросаешься к выходу, но через несколько шагов падаешь на пол, держась за горло.
p Там ты и умираешь, отравившись дымом из сломанной печки.
end

:13b
p Зачем? Печь ведь все равно неисправна.
btn 7, Продолжить ремонт
end

:14
pln Эта старая дырка в полу всегда мешала, ты уже не помнишь откуда она появилась. Наверное доски прогнили.
p Ты приносишь из сарая несколько новых досок, молоток и гвозди. Пара сильных ударов и....молоток проламывает еще одну доску и, вырвавшись из твоих рук, падает в образовавшуюся дыру.
btn 7, Продолжить ремонт
btn 14a, Заглянуть в дыру
end

:14a
p Глубокая дыра в полу. Глубокая и темная. Ты ничего не можешь разглядеть.
btn 7, Продолжить ремонт
if Коробок спичек then btn 16,Использовать спички
if not Коробок спичек and Записка then btn 16,Пошарить в дыре
end

:15
pln Перепачкавшись сам и перепачкав все вокруг ты наконец-то заканчиваешь покраску стен. Только вот, что это за странный цвет получился? Ну что же, придется красить снова.
btn 7,Вернуться
end

:16
pln Ты зажигаешь спичку. Теперь ты можешь разглядеть дыру в полу. Она достаточно глубокая. На дне ты видишь свой молоток и какой-то сверток.
btn 7, Продолжить ремонт
if Молоток and not Сверток and not Записка and not Шкатулка then cls & p Перед тобой глубокая дыра в полу. На дне лежит какой-то сверток.
if not Сверток and not Записка and not Шкатулка then btn 17, Достать сверток
if not Молоток then btn 16a, Взять молоток
if Сверток then cls & p Перед тобой пустая дыра в полу. В руках ты держишь замотанную в кусок старой пыльной ткани шкатулку.
if Сверток and not Молоток then btn 7, Продолжить ремонт & btn 16a, Взять молоток
if Сверток and not Шкатулка and not Записка then btn 19, Выйти из дома & btn 17a, Взять шкатулку
if Сверток and not Кусок ткани then btn 17b, Взять кусок ткани
if Шкатулка and Кусок ткани then inv- Сверток
if Шкатулка then btn 18,Рассмотреть шкатулку & 18a, Потрясти шкатулку
end

:16a
p Ты взял молоток.
inv+ Молоток & cls
goto 16

:17
p Ты достаешь сверток и видишь, что это небольшая шкатулка, завернутая в кусок старой пыльной ткани.
inv+ Сверток
goto 16

:17b
p Ты взял кусок старой пыльной ткани.
inv+ Кусок ткани & cls
goto 16

:17a
p Ты взял небольшую шкатулку.
inv+ Шкатулка & cls
goto 16

:18
pln Перед тобой неописуемой красоты шкатулка сделанная из прочного дерева мелорна и украшенная крупными драгоценными камнями.
p Она явно сделана руками эльфов.
btn 18b,Открыть шкатулку
btn 18a,Потрясти шкатулку
end

:18a
pln Ты трясешь шкатулку, прислонив ее к уху. Внезапно раздается громкий щелчок, и шкатулка взрывается у тебя в руках.
p Эльфийские мастера всегда умели делать хитроумные ловушки для глупых орков.
end

:18b
pln Ты не видишь ни замочной скважины, ни даже щели. Шкатулка выглядит совершенно плоской. Наверное это действие какого-то заклинания.
p "Нужно будет показать её какому-нибудь магу." - думаешь ты.
btn 16, Вернуться
end

:19
pln Ты стоишь во дворе своей усадьбы. Теплое летнее солнце ярко освещает, расположенный вокруг твоего дома сад. Выложенная камнями дорога уводит за ворота.
pln Справа от дома расположен, покрашенный желтой краской, сарай.
btn 22,Выйти за ворота
btn 6,Войти в дом
btn 20,Идти в сарай
end

:20
pln Дверь со скрипом открывается, и ты входишь в сарай. На расположенных слева и справа полках лежат старые, ненужные вещи. В дальнем углу сарая стоит высокий деревянный шкаф. По полу разбросаны садовые инструменты.
if not Бутылка вина then p На шкафу ты видишь зеленую бутылку.
btn 19,Выйти из сарая
if not Банка с крышкой and not Факел and not Принести тряпку and not Принести деревяшку and not Принести масло then btn 20a,Осмотреть полки
if not Бутылка вина then btn 20b,Достать бутылку
btn 21,Открыть шкаф
if Лестница and not Бутылка вина and not Пустая бутылка then btn 20c, Использовать лестницу
end

:20a
pln Внимательно осмотрев все полки, ты находишь металлическую банку.
if Банка с маслом then pln На полках ничего нет. & btn 20, Вернуться
if not Банка с маслом then btn 200a, Открыть банку
end

:200a
p Ты снимаешь с банки металлическую крышку и заглядываешь внутрь. Там лежат 20 монет. Когда-то давно ты положил их в туда, а потом забыл про них.
inv+ 20,Монет & cls
p Ты решил прихватить заодно и банку.
inv+ Банка с крышкой & cls
goto 20

:20b
pln Ты не можешь дотянуться до бутылки. Тогда ты решаешь вскарабкаться вверх по полкам и достать её. Ты осторожно лезешь вверх, но внезапно оступаешься, и падаешь вниз, прямо на лежащие на полу грабли.
p Твое окровавленное тело, лежащее в дальнем углу сарая, найдут еще не скоро.
end

:20c
p Ты подставляешь складную лестницу к шкафу, влезаешь на нее и достаешь зеленую бутылку, закрытую пробкой.
btn 20d,Открыть бутылку
btn 20e,Взять бутылку
end

:20d
p Как только ты открываешь бутылку, тебе в нос ударяет резкий запах вина. Это очень хорошее и крепкое вино. 
btn 20e,Взять бутылку
end

:20e
pln Ты взял бутылку с вином.
inv+ Бутылка вина
p Тяжелую складную лестницу ты решил оставить здесь, в сарае.
inv- Лестница & cls
goto 20

:21
pln Порывшись в шкафу ты находишь старый дорожный плащ и прочный кожанный пояс.
btn 20,Вернуться
if not Дорожный плащ then btn 21a,Взять дорожный плащ
if not Кожанный пояс then btn 21b,Взять кожанный пояс
if Дорожный плащ and Кожанный пояс then cls & p В шкафу ничего нет. & btn 20,Вернуться
end

:21a
p Ты взял старый дорожный плащ.
inv+ Дорожный плащ & cls
goto 21

:21b
p Ты взял прочный кожанный пояс.
inv+ Кожанный пояс & cls
goto 21

:22
pln Ты выходишь на городскую площадь. Прямо перед тобой расположено здание городского склада. Недалеко от входа на склад расположен колодец.
pln Справа виднеется крыша "Зеленого холма" - лучшего трактира в Уделе. А слева расположен, огороженный высоки забором, дом твоего соседа - Сэма.
p Небольшая тропинка уводит в густой лес, растущий за твоей усадьбой.
btn 19,Идти в Бебень
btn 23,Идти на склад
btn 25,Идти к колодцу
btn 26,Идти к соседу
btn 29,Идти в трактир
btn 40,Идти в лес
btn Save2,Сохранить игру
end

:Save2
pln Не все так просто. Чтобы сохранить игру тебе придется заплатить 1 монету.
pln
p Согласен?
if not Монет then btn 22,У тебя нет денег!
if Монет then btn Save,Да & btn 22,Нет
end

:Save
inv- Монет
save 22
cls
goto 22

:23
p Ты заходишь на склад. К тебе подходит пожилой хоббит в рабочей одежде. "Привет! Меня зовут Ганс. Вы хотите что-нибудь купить?" - говорит он.
btn 22,"Нет, спасибо."
btn 24,"А что у вас есть?"
end

:24
pln У нас есть:
pln Деревянный ящик за 5 монет.
pln Складная лестница за 10 монет.
pln Пустая бутылка за 30 монет.
pln Эльфийская лодка за 120 монет.
btn 22, Уйти со склада
if 5 Монет and not Ящик and not Доски and not Деревяшка and not Факел then btn 24a,Купить ящик за 5 монет
if 10 Монет and not Лестница and not Бутылка вина and not Бутылка с водой and not Пустая бутылка then btn 24b,Купить лестницу за 10 монет
if 30 Монет and not Пустая бутылка and not Старая монета and not Бутылка с водой and not Бутылка вина then btn 24d,Купить Бутылку за 30 монет
if 120 Монет and not Лодка then btn 24c,Купить лодку за 120 монет
if Ящик then btn 24e,Продать ящик за 5 монет
if Пустая бутылка then btn 24s,Продать бутылку за 30 монет
end

:24a
inv- 5, Монет
p Ты купил деревянный ящик.
inv+ Ящик & cls
goto 24

:24b
inv- 10, Монет
p Ты купил складную лестницу.
inv+ Лестница & cls
goto 24

:24c
inv- 120, Монет
p Ты купил прочную и легкую эльфийскую лодку.
inv+ Лодка & cls
goto 24

:24d
inv- 30, Монет
p Ты купил пустую стеклянную бутылку.
inv+ Пустая Бутылка & cls
goto 24

:24e
inv- Ящик
p Ты продал деревянный ящик.
inv+ 5,Монет & cls
goto 24

:24s
inv- Пустая бутылка
p Ты продал пустую бутылку.
inv+ 30, Монет & cls
goto 24

:25
p Ты стоишь перед глубоким колодцем. Далеко внизу слышен плеск воды. Ты заглядываешь в темный колодец, но ничего не можешь разглядеть. Привязанное веревкой ведро стоит на краю колодца.
btn 22,Вернуться на площадь
btn 25a,Набрать воды
if Коробок спичек then btn 25b,Зажечь спичку
if Факел then btn 25b,Зажечь факел
btn 25c,Спуститься вниз по веревке
btn 25d,Прыгнуть в колодец
if Банка с крышкой then btn 25e,Налить воду в банку
if Пустая бутылка and not Бутылка с водой then btn 25k,Налить воду в бутылку
end

:25a
pln Ты опускаешь пустое ведро в колодец, а когда достаешь его ты видишь, что оно наполнено свежей холодной водой.
pln Ты был бы рад взять немного этой воды с собой, но к сожалению, тебе ее некуда налить.
btn 25,Вернуться
end

:25b
p Ты зажигаешь спичку и сразу же бросаешь ее в колодец. Она медленно падает вниз, постепенно сгорая. Ты успеваешь заметить что-то блестящее на дне колодца.
btn 25,Вернуться
btn 25c,Спуститься вниз по веревке
btn 25d,Прыгнуть в колодец
end

:25c
pln Ты начинаешь медленно и осторожно спускаться в колодец, ухватившись за веревку. Ты успеваешь спуститься почти до середины, когда внезапно веревка лопается, не выдержав твоего веса.
pln Ты падаешь в воду. Ближайшие несколько дней, ты тщетно пытаешься, вылезти из колодца, звать на помощь, кричать. В конце-концов ты умираешь от голода и переохлаждения.
end

:25d
pln Ты встаешь на край колодца и прыгаешь вниз в темноту.
pln Во время падения твое тело бьется о бревна, из которых сложен колодец. И вот оно безжизненное и окровавленное плюхается в воду.....Больше никто и никогда не будет пить воду из этого колодца.
end

:25e
pln Ты наливаешь воду в металлическую банку и закрываешь её крышкой. Но крышка прилегает не плотно, и поэтому тебе не удасться уйти далеко и не расплескать воду.
p Попробуй найти для воды другой сосуд.
btn 25,Вернуться
end

:25k
p Набрав полное ведро воды, ты переливаешь её в пустую бутылку.
inv- Пустая бутылка
inv+ Бутылка с водой & cls
goto 25

:26
p Ты стоишь перед воротами дома Сэма. Его окружает высокий и плотный забор, через который ничего нельзя разглядеть - Сэм был хорошим плотником пока не спился. На ворота приклеен бумажный листок.
btn 22,Вернуться на площадь
btn 26a,Постучать в ворота
btn 26b,Перелезть через забор
btn 26c,Прочитать надпись на листке
end

:26a
pln Ты стучишь в ворота - никакого ответа. Ты стучишь еще громче, и слышишь раздавшийся из-за ворот голос Сэма: "Входите, открыто!"
p Ты дергаешь ручку ворот, но они оказываются заперты.
btn 26,Вернуться
end

:26b
pln Ты подтягиваешься на руках, уцепившись за верхний край забора. Немного попотев тебе удается влезть на забор, а потом спрыгнуть во двор дома Сэма. Никаких собак не видно.
p Перед тобой две тропинки. Первая ведет ко входу в дом, а вторая огибает дом слева.
btn 26,Перелезть обратно
btn 26s,Идти ко входу в дом
btn 27,Обойти дом сзади
end

:26c
pln На листке написано: "Осторожно, во дворе злые собаки!!!"
p "Может сегодня не стоит идти к соседу?" - думаешь ты.
btn 26,Вернуться
end

:26s
p Ты направляешься к главному входу в дом Сэма. Внезапно из дверей выскакивают две огромные собаки и набрасываются на тебя. Подбежавший слишком поздно Сэм, громко крича, оттаскивает их от твоего обглоданного трупа.
end

:27
pln Ты неспеша идешь вдоль дома, обходишь его сзади и подходишь ко второму входу в дом. На открытой летней веранде в кресле-качалке сидит Сэм и читает газету. Его красное лицо говорит о том, что он как всегда прикладывался к бутылке с самого утра.
p "А, привет Фред! Как дела?" - говорит он повернувшись к тебе и откладывая газету в сторону.
if Бутылка вина then cls & goto 28
if Пустая бутылка then cls & p На открытой летней веранде в кресле-качалке спит Сэм. & btn 270,Разбудить Сэма
btn 26,Выйти за ворота
btn 27a, "Привет Сэм, все нормально."
if Доски and not Деревяшка then btn 260,Показать Сэму доски
if Шкатулка then btn 27d,Показать Сэму шкатулку
end

:260
pln Ты показываешь Сэму доски. На его лице внезапно появляется улыбка, он словно бы молодеет, вспоминая, что когда-то он был неплохим плотником.
p "Спасибо, Фред, ты напомнил мне о моей молодости. Я ведь ещё не забыл своё мастерство. Хочешь я что-нибудь сделаю для тебя из этих досок? Тебе что-нибудь нужно?" - спрашивает Сэм.
btn 27,"Нет, спасибо."
if Принести деревяшку then btn 245,"Да, нужно."
end

:245
pln - Не мог бы ты сделать мне ручку для факела? - спрашиваешь ты.
pln - Конечно! Я могу! Я правда могу! - отвечает Сэм. Он хватает доски и убегает к себе в мастерскую.
inv- Доски
pln Через некоторое время он возвращается, неся в руках гладкую и длинную деревяшку.
p - Вот, возьми. - говорит он и с гордостью протягивает её тебе.
btn 271,Взять деревяшку
end

:271
p Ты взял гладкую и длинную деревяшку.
inv+ Деревяшка & cls
goto 27

:270
p Ты трясёшь спящего Сэма за плечо. Слегка приоткрыв свои пьяные глаза, он говорит заплетающимся языком: "Убирайся отсюда, а не то я спущу собак!"
btn 26,Выйти за ворота
btn 275,Встряхнуть Сэма
if Доски then btn 260,Показать Сэму доски
end

:275
p Ты хватаешь Сэма за плечи и трясешь его изо всех сил, надеясь на то, что он протрезвеет. "ФАААААС!!!" - внезапно орёт он, и, из дверей выскакивают две огромные собаки и набрасываются на тебя. Когда Сэм проснётся и увидит, что он сделал, он закопает твой труп в своём саду.
end

:27a
pln - А, привет, Фред! Как дела?
pln - Привет Сэм, все нормально.
pln - У меня к тебе небольшая просьба, Фред. Ну....Ты же ведь знаешь мою жену, правда? Она опять спрятала все мое вино. Ты не мог бы принести мне чего-нибудь выпить, Фред? Я тебе за это заплачу!
btn 27b,"Нет, я не могу."
btn 27c,"Да, конечно, Сэм."
end

:27b
pln Пьяный Сэм, разочарованно смотрит на тебя. Внезапно он хватает лежащую под креслом пустую бутылку, и бросает её в тебя со словами "Тогда я убью тебя сам !!!"
p Ты не успеваешь увернуться и бутылка попадает тебе в голову. Последнее, что ты слышишь - это пьяный и безумный хохот Сэма.
end

:27c
pln - Да, конечно, Сэм, я принесу тебе выпить.
p - Спасибо! Только возвращайся как можно скорее. Я не могу терпеть - трубы горят.
btn 27,Вернуться
if Бутылка вина then btn 28,Дать Сэму вино
if Стакан лимонада then btn 270a,Дать Сэму лимонад
if Кружка пива then btn 270b,Дать Сэму пиво
if Коктейль then btn 270c,Дать Сэму коктейль
end

:270a
p Ты даешь Сэму лимонад. "Что за дерьмо ты мне принес?" - восклицает он, и бросает стакан с лимонадом в траву.
inv- Стакан лимонада
btn 27c,Вернуться
end

:270b
p Ты даешь Сэму кружку пива. Он одним залпом выпивает её. "Неплохо, но это не то, что я хотел." - говорит он, и вновь принимается читать газету.
inv- Кружка пива
btn 27c,Вернуться
end

:270c
p Ты даешь Сэму коктейль. Он сначала нюхает его, потом осторожно пробует. "Тьфу, какая гадость!" - говорит он отплевываясь. - "Принеси мне лучше вина!"
inv- Коктейль
btn 27c,Вернуться
end

:27d
p Ты показываешь Сэму шкатулку. "Вот это да! Её можно обменять на целый ящик вина, а то и на два!!!" - восклицает он.
btn 27,Вернуться
end

:28
p Сэм все так же сидит в кресле-качалке. "Ну что, принес выпить?" - спрашивает он.
btn 28a,"Да, держи!"
btn 28b,"Да, где деньги?"
end

:28a
p Ты даешь Сэму бутылку вина, он жадно выпивает её и .... засыпает. Пустая бутылка из-под вина падает на пол веранды. Никаких денег он тебе конечно же не даёт.
inv- Бутылка вина
btn 28c,Взять пустую бутылку
end

:28b
pln - Да, где деньги? - спрашиваешь ты.
p - Вот возьми. - говорит Сэм и протягивает тебе 30 монет.
inv- Бутылка вина
inv+ 30,Монет
btn 26,Вернуться за ворота
end

:28c
p Ты взял пустую бутылку.
inv+ Пустая бутылка & cls
goto 27

:29
p Ты стоишь перед входом в трактир. Над входом висит, качающаяся на ветру, вывеска "Зеленый холм". У самых дверей сидит одетый в лохмотья старик. Он протягивает руку прося милостыню.
btn 22,Вернуться на площадь
if Монет then btn 29a,Дать старику 1 монету
if 5 Монет and not Игральная карта and not Пустая шкатулка and not Записка then btn 29b,Дать старику 5 монет
btn 30,Войти в трактир
end

:29a
p Ты протягиваешь старику 1 монету. "Да хранит тебя господь, добрый хоббит." - говорит он.
inv- Монет
btn 29,Вернуться
end

:29b
p Ты протягиваешь старику 5 монет. "Спасибо тебе за твою щедрость. Взамен я дам тебе совет: подойди к человеку сидящему в углу трактира и дай ему эту игральную карту. Он поможет тебе." - говорит старик.
inv- 5,Монет
inv+ Игральная карта
btn 29,Вернуться
end

:30
pln В трактире в столь ранний час не так много посетителей. За столиками сидят несколько знакомых хоббитов. В дальнем углу, возле окна, сидит незнакомый человек в низко надвинутой на глаза широкополой шляпе.
p За стойкой бара ты видишь Фалина - добродушного бородатого гнома.
btn 29,Выйти из трактира
btn 30a,Поговорить с хоббитами
btn 30b,Поговорить с барменом
if not Древний кинжал then btn 31,Подойти к человеку в углу
end

:30a
pln Ты подсаживаешься к своим знакомым хоббитам, и каждый норови угостить тебя кружкой-другой пива. Отказываться неудобно, и поэтому приходиться пить их одну за другой.
p Незаметно наступает вечер. Тебя пьяного в стельку приносят домой. Ты еще помнишь, как продавал все свои вещи, а на вырученные деньги угощал посетителей трактира.
end

:30b
p Ты сидишь перед стойкой бара. Гном Фалин поворачивает к тебе свое веселое, улыбающееся лицо. "Что будешь пить, Фред?" - спрашивает он.
btn 30, "Спасибо, ничего."
if Монет then btn 300a,Стакан лимонада - 1 монета
if 5 Монет then btn 300b,Кружка пива - 5 монет
if 10 Монет then btn 300c,Коктейль - 10 монет
if Пустая шкатулка then btn 300d,Продать шкатулку
if Птичьи яйца then btn 300h,Продать яйца
end

:300a
inv- 1, Монет
p Ты взял стакан лимонада.
inv+ Стакан лимонада & cls
goto 30b

:300b
inv- 5, Монет
p Ты взял кружку пива.
inv+ Кружка пива & cls
goto 30b

:300c
inv- 10, Монет
p Ты взял коктейль.
inv+ Коктейль & cls
goto 30b

:300d
pln Зная, что все гномы обожают драгоценные камни, ты решаешь продать больше ненужную шкатулку Фалину.
pln "Это же просто сокровище!!!" - говорит гном. "Я дам тебе за нее 150 монет. Договорились?"
btn 300e, Да
btn 30, Нет
end

:300e
p Ты продал шкатулку.
inv+ 150,Монет
inv- Пустая шкатулка & cls
goto 30

:300h
pln "Какие восхитительные яйца!" - восклицает Фалин. - "Я смогу приготовить из них наивкуснейший омлет.
pln Но к сожалению у меня сегодня плохая выручка, т.к. в трактире мало посетителей. Я не смогу заплатить тебе за эти изумительные птичьи яйца.
p Но я могу дать тебе взамен немного масла, если хочешь?"
btn 30b,"Нет, не надо."
btn 300g,"Хорошо, давай масло."
end

:300g
pln - Хорошо, давай масло. - соглашаешься ты.
p - У меня, к сожалению, некуда его налить. У тебя есть какая-нибудь посуда? - разачарованно вздыхает гном.
btn 30b,"Пойду поищу."
if Банка с крышкой then btn 300k,"Да, у меня есть банка."
end

:300k
p Ты отдаешь птичьи яйца бармену...
inv- Птичьи яйца & cls
p ...и он наливает масло в твою железную банку.
inv- Банка с крышкой
inv+ Банка с маслом & cls
goto 30b

:31
pln Ты подходишь к человеку, сидящему в углу. Он поднимает голову и смотрит на тебя. Ты видишь его голубые глаза, наполненные мудростью и несгибаемой волей.
p "Кто ты такой?" - спрашивает он, глядя на тебя в упор.
btn 30,Отойти от человека
btn 31a,"Не твое дело."
btn 31b,"А ты кто такой?."
btn 31c,"Ты знаешь старика у входа?"
btn 31d,"Меня зовут Фред."
if Шкатулка then btn 31h,Показать шкатулку
if Игральная карта then btn 31e,Показать игральную карту
end

:31a
pln Незнакомец вскакивает из-за стола. Он словно бы вырастает и возвышается над тобой. "Ты хоть знаешь с кем говоришь, о презренный?" - грохочет он.
pln Он протягивает руку в твою стророну. Внезапно с его ладони срывается огненный шар и ударяет в тебя. Твоя одежда вспыхивает, ты громко кричишь. Твои знакомые хоббиты, вскакивают из-за столов и в страхе выбегают на улицу. Ты мечешься по трактиру, пытаясь сбить с себя пламя, но это тебе не удаётся.
p Незнакомец уходит, а твой обгоревший труп лежит на полу, слегка дымясь.
end

:31b
pln "Невежливо отвечать вопросом на вопрос." - говорит незнакомец. - "Ну хорошо, я представлюсь первым, меня зовут Брендальф, я великий маг из Ангмара, член Белого Ордена."
p "Теперь ты скажешь мне своё имя?" - спрашивает Брендальф.
btn 31d,"Меня зовут Фред."
end

:31c
p "Может быть да, а может и нет." - неопределенно отвечает человек в шляпе.
btn 31,Вернуться
end

:31d
p "Очень приятно." - говорит незнакомец, продолжая пить пиво и не замечая тебя.
btn 31,Вернуться
if Шкатулка then btn 31h,Показать шкатулку
end

:31h
pln Когда ты достаешь шкатулку и показываешь ее незнакомцу, его голубые глаза грозно вспыхивают. "Это не принадлежит тебе!" - говорит он, и забирает у тебя шкатулку.
p Его грозный вид пугает тебя. Ты невнятно бормочешь себе под нос ругательства, и медленно отходишь от незнакомца, решив не спорить с ним.
inv- Шкатулка
btn 30,Отойти от незнакомца
if Игральная карта then btn 31e,Показать игральную карту
end

:31e
p Незнакомец берет карту из твоих рук, внимательно смотрит на нее. "Я вижу, что твое сердце наполнено добротой и теплом. Я помогу тебе. Что ты хочешь?" - говорит он.
inv- Игральная карта
if Шкатулка then btn 32,Спросить о шкатулке
if not Шкатулка then btn 33,Забрать шкатулку назад
end

:33
p Незнакомец возвращает тебе шкатулку.
inv+ Шкатулка
btn 32,Спросить о шкатулке
end

:32
pln "Я нашел эту шкатулку под полом в своей усадьбе. Она выглядит очень древней. Я пытался открыть её, но у меня ничего не вышло. Если вы маг, то может быть вы поможете мне открыть её?" - говоришь ты.
pln Брендальф внимательно смотрит на шкатулку, а потом произносит несколько слов на непонятном тебе языке. Шкатулка на мгновение вспыхивает ярким светом. "Я снял с нее эльфийское заклинание блокировки. Теперь она открыта." - говорит маг, указывая на шкатулку.
p "Огромное спасибо!" - говоришь ты.
btn 34,Открыть шкатулку
end

:34
p Ты заглядываешь внутрь шкатулки и видишь, лежащие в ней конверт и небольшой кинжал дивной работы, украшенный незнакомыми древними рунами.
inv- Шкатулка
if Записка then btn 34d,Прочитать записку
if not Пустая шкатулка then btn 34a,Взять пустую шкатулку
if not Записка then btn 34b,Открыть конверт
if not Древний кинжал then btn 34c,Взять кинжал
end

:34a
p Теперь шкатулка пуста.
inv+ Пустая шкатулка & cls
goto 34

:34b
p Ты открыл конверт и обнаружил лежащую там записку. Ты взял её.
inv+ Записка & cls
goto 34

:34c
p Ты взял древний кинжал испещренный незнакомыми письменами.
inv+ Древний кинжал & cls
goto 34

:34d
p Текст записки написан на незнакомом вам языке. Буквы не похожи ни на эльфийские руны, ни на алфавит общего наречия.
if Записка and Пустая шкатулка and Древний кинжал then btn 35,Показать записку магу
btn 34,Вернуться
end

:35
pln Брендальф смотрит на записку, его глаза хмурятся. "Это древний язык гномов." - наконец говорит он. "Вот, что там написано:
pln Фолин Великий нашел этот кинжал в подземной усыпальнице расположенной под Ортханком. Он был вложен в руку мертвого человека, одетого в черный балахон, поверх дорогих доспехов. Как выяснилось позже, это был труп одного из Девяти кольценосцев (Чёрного всадника, назгула - так называли его другие народы).
pln Кем и когда он был похоронен там, так и не удалось выяснить. На срочно созванном совете было решено заключить эту злую реликвию в шкатулку, наложив на неё заклинание блокировки, и отдать на хранение хоббиту Фолко, прямому наследнику Хранителя кольца - Фродо.
p Если эта шкатулка попадёт в чужие руки просьба немедленно сообщить об этом в Разлог, правящему там в тот момент владыке."
btn 30,Уйти от Брендальфа
btn 35a,"Что мне делать?"
btn 35b,"Ты веришь в это?"
end

:35a
pln - Что мне делать, Брендальф? - спрашиваешь ты.
p - Я думаю, что ты должен исполнить то, что сказано в записке. Ты должен отнести это в Разлог. - отвечает маг.
btn 35,Вернуться
end

:35b
pln - И ты во всё это веришь? - удивлённо спрашиваешь ты.
pln - Эльфийские чары, древний язык гномов, Чёрный всадник.... Это не может быть просто совпадением. - серьёзно отвечает маг.
p "Раз эта шкатулка принадлежала моему дальнему предку, то теперь она по праву моя..." - рассуждаешь ты - "...и я должен выполнить предписание и отнести её в Разлог."
btn 35,Вернуться
end

:40
pln Ты входишь в густой зеленый лес. Огромные деревья нависают над тропой, ветер шелестит их листвой. Воздух вокруг наполнен ароматом цветов и трав.
p Перед тобой развилка с дорожным указателем. Одна дорога ведет на север, другая на восток.
btn 22,Вернуться на площадь
btn 41,Идти на север
btn 43,Идти на восток
btn 40a,Рассмотреть указатель
end

:40a
pln На покосившемся от времени дорожном указателе видна плохо различимая надпись:
pln "Север - заброшенная шахта. Опасно! Не входить!!!
p "Восток - дом лесника."
btn 40,Вернуться
end

:41
p Ты стоишь перед огромной скалой. В её основании расположен вход в пещеру. Оттуда веет сыростью и холодом.
btn 40,Вернуться к развилке
btn 42,Войти в пещеру
btn 41a,Влезть на скалу
end

:41a
pln Ты лезешь вверх, цепляясь за выступы и торчащие из скалы корни. Через несколько минут ды достигаешь небольшого уступа.
p Решив немного передохнуть, ты влезаешь на уступ и тут замечаешь большое гнездо, скрытое в траве.
if Птичьи яйца then cls & p Ты стоишь на небольшом уступе откуда открывается прекрасный вид на окрестности. В траве ты видишь пустое птичье гнездо.
btn 41,Спуститься вниз
btn 41c,Сбросить гнездо со скалы
if not Птичьи яйца then btn 41b,Заглянуть в гнездо
if Ящик and not Доски and not Деревяшка and not Факел then btn 410,Сбросить ящик со скалы
end

:410
p Ты подходишь к краю уступа и бросаешь деревянный ящик вниз. Он падает со скалы, ударяясь об острые камни.
inv- Ящик
btn 410a,Спуститься вниз
end

:410a
p Ты спускаешься вниз и видишь, что от ящика осталась лишь груда сломанных досок.
btn 410b,Взять доски
end

:410b
p Ты взял несколько небольших досок.
inv+ Доски & cls
goto 41

:41c
pln Ты сбрасываешь гнездо со скалы и смотришь, как оно падает вниз. Внезапнов небе раздаётся громкий птичий крик. Ты поднимаешь голову и видишь, летящего прямо на тебя, гигантского орла.
pln В ужасе ты пытаешься закрыть голову руками, но громадная птица набрасывается на тебя выпустив вперёд острые когти.
p Теперь стервятникам будет, чем подкрепиться.
end

:41b
p Ты заглядываешь в гнездо и видишь лежащие там крупные птичьи яйца.
btn 41,Спуститься вниз
btn 41с,Сбросить гнездо со скалы
btn 41d,Взять яйца
end

:41d
p Ты взял птичьи яйца.
inv+ Птичьи яйца & cls
goto 41a

:42
p Ты стоишь внутри пещеры. Здесь очень холодно, сыро и темно. Ты ничего не видишь в темноте, но где-то вдалеке слышишь плеск воды.
btn 41, Выйти из пещеры
if Факел then btn 47,Зажечь факел
if not Факел then btn 42a,Идти вглубь пещеры
if Коробок спичек then btn 42b,Зажечь спичку
end

:42a
pln Ты в полной темноте идешь на шум воды, держась руками за стену пещеры. Внезапно, ты ощущаешь под ногами пустоту и, не удержавшись, падаешь в глубокую яму с острыми камнями на дне.
p Твое тело будет лежать там целую вечность, потому что его никто и никогда не найдет.
end

:42b
pln Ты чиркаешь спичкой, но внезапный порыв ветра тушит её. Ты успеваешь заметить два туннеля, уводящие вглубь пещеры.
p Ты пытаешься зажечь другие спички, но из-за большой влажности они отсырели. Ты выбрасываешь ненужный больше коробок.
inv- Коробок спичек
btn 42,Вернуться
end

:43
pln Ты стоишь перед невысокой лесной хижиной с соломенной крышей. Насколько тебе известно в ней живет одинокий лесник Терри. Он недолюбливает и сторонится своих сородичей - хоббитов, но зато он просто обожает всевозможные растения.
p Перед его хижиной раскинулся чудесный сад и несколько клумб, усаженных яркими цветами.
btn 40,Вернуться к развилке
btn 43a,Сорвать цветок
btn 44,Войти в хижину
end

:43a
pln Ты наклоняешься к клумбе и срываешь ярко желтый цветок необычайной красоты. Внезапно ты слышишь звук тетивы, и спустя секунду стрела, выпущенная Терри, вонзается тебе в горло.
p "НЕ СМЕЙ ТРОГАТЬ МОИ ЦВЕТЫ !!!" - бешенно орёт он. Алые капли твоей крови падают на нераспустившиеся бутоны прекрасных цветов.
end

:44
p Ты в хижине лесника Терри. Здесь очень тепло. Яркий свет, висящих на стенах факелов, освещает стоящие повсюду горшки с цветами. Терри, склонившись над одним из цветков, поливает его странной сетло-голубой жидкостью.
btn 43,Выйти из хижины
btn 44a,Поздороваться
btn 44b,Спросить про жидкость
if not Коробок спичек and not Факел then btn 44c,Попросить факел
if Старая монета and not Факел then btn 45a,Сделать факел.
end

:44a
pln - Здравствуй, Терри. Чем это ты занимаешься? - говоришь ты.
p - Не видишь что ли? Я поливаю свою прелесть! - недовольно отвечает он, продолжая поливать цветок и отвернувшись от тебя.
btn 44,Вернуться
end

:44b
pln - А чем это ты поливаешь цветы? Наверное, это какое-нибудь специальное средство? - с видом знатока спрашиваешь ты.
pln - О да, это самое лучшее средство... - сразу оживляется он. - Я сам изобрёл его.
pln - И из чего ты его делаешь? - говоришь ты.
p - Я готовлю его из.... Кстати, я совсем забыл - у меня как раз закончилась вода из городского колодца. А это и есть один из компонентов моего чудо-удобрения. Не мог бы ты принести мне немного этой воды? - спрашивает Терри.
btn 44,"Нет, я очень занят."
btn 44c,"Хорошо, а что взамен?"
end

:44c
pln - Не мог бы ты дать мне один из факелов? - спрашиваешь ты.
p - Если ты поможешь мне, то я с радостью подарю тебе один. - отвечает Терри, хитро улыбаясь.
btn 44b,"Чем я могу помочь?"
if not Бутылка с водой then btn 44,"Ладно, я постараюсь."
if Бутылка с водой then btn 44d,Отдать бутылку с водой.
end

:44d
pln - Вот, я принес воду. - говоришь ты и протягиваешь Терри бутылку с водой.
inv- Бутылка с водой
pln - Теперь ты дашь мне факел? - спрашиваешь ты.
p - Я бы с радостью отдал тебе факел, но....у меня так много цветов....и им всем нужны свет и тепло....
if not Старая монета then btn 45,"ГДЕ МОЙ ФАКЕЛ?"
if Старая монета then cls & p - Спасибо. - говорит Терри. & btn 44,Вернуться
end

:45
pln - ГДЕ МОЙ ФАКЕЛ? - громко орешь ты.
pln - Ладно-ладно, вот, возьми эту старую монету за труды. И, если хочешь, я могу сделать тебе новый факел. Для этого мне понадобятся: сухая тряпка, немного масла и деревяшка для ручки. - испуганно бормочет Терри.
pln
p Ты взял у Терри старую монету.
inv+ Старая монета
inv+ Принести тряпку
inv+ Принести масло
inv+ Принести деревяшку
btn 44,Вернуться
if not Факел then btn 45a,"У меня кое-что есть."
end

:45a
pln "Давай посмотрим, что у тебя есть." - говорит Терри.
btn 44,Вернуться
if Факел then pln У тебя уже есть факел & btn 43,Выйти из хижины
if Кусок ткани and Принести тряпку then btn 45b,Отдать кусок ткани
if Банка с маслом and Принести масло then btn 45c,Отдать банку с маслом
if Деревяшка and Принести деревяшку then btn 45d,Отдать деревяшку
if not Принести тряпку and not Принести масло and not Принести деревяшку and not Кусок ткани and not Банка с маслом and not Деревяшка and not Факел then btn 46,"Я принес все предметы."
end

:45b
pln Ты отдаёшь кусок ткани Терри.
inv- Кусок ткани
p "Принеси мне остальные вещи и я сделаю для тебя факел" - говорит Терри.
inv- Принести тряпку & cls
goto 45a

:45c
pln Ты отдаёшь банку с маслом Терри.
inv- Банка с маслом
p "Принеси мне остальные вещи и я сделаю для тебя факел" - говорит Терри.
inv- Принести масло & cls
goto 45a

:45d
pln Ты отдаёшь деревяшку Терри.
inv- Деревяшка
p "Принеси мне остальные вещи и я сделаю для тебя факел" - говорит Терри.
inv- Принести деревяшку & cls
goto 45a

:46
pln - Я принес все предметы, которые ты просил, Терри. - говоришь ты.
pln - Да, я вижу. Теперь я сделаю для тебе факел, подожди несколько минут.
pln Терри ушел, и через некоторое время вернулся с только что изготовленным факелом.
p - Вот, готово. - говорит он и протягивает тебе факел.
btn 46a,Взять факел
end

:46a
p Ты взял факел. К его верхней части прикреплен кусок кремня, так что ты легко сможешь зажечь этот факел без спичек.
inv+ Факел & cls
goto 44

:47
pln Ты зажигаешь факел, чиркнув кремнем по каменной поверхности пещеры, и поднимаешь его над головой.
pln Теперь ты можешь разглядеть всё вокруг. За твоей спиной - проход ведущий к выходу из пещеры, а прямо перед собой ты видишь два темных туннеля.
p Прислушавшись, ты определяешь, что шум воды доносится из туннеля расположенного слева.
btn 41,Выйти из пещеры
btn 48,Идти в левый туннель
btn 47b,Идти в правый туннель
end

:47b
pln Ты входишь в правый туннель. С каждым шагом, уводящим тебя в его глубину, ты всё явственнее ощущаешь мерзкий запах гнили. Когда зловоние становится невыносимым, ты решаешь повернуть назад.
p Внезапно, сверху на тебя падает ужасная тварь, похожая на паука, но с парой огромных, светящихся в темноте клыков. Её волосатые лапы хватают тебя, клыки впиваются в горло, и ты захлёбываешься собственной кровью.
end

:48
pln Пройдя несколько шагов по туннелю, ты попадаешь в небольшую пещеру. Её свод украшен высеченными в камне древними рунами.
p В центре пещеры ты видишь стоящую на небольшом возвышении ржавую вагонетку. Железные рельсы уводят в темный туннель, из которого слышен плеск бегущей воды.
btn 47,Вернуться в туннель
btn 48a,Рассмотреть руны
btn 48b,Рассмотреть вагонетку
btn 48c,Использовать вагонетку
btn 48d,Исследовать туннель
if Вагонетка исправна then btn 50t,Разогнать вагонетку
if Лодка and Вагонетка исправна then btn 470,Погрузить лодку
end

:470
p Ты кладешь эльфийскую лодку в вагонетку. Потом ты залезаешь туда сам.
btn 48,Вернуться
btn 470a,Оттолкнуться ногой
if Кожанный пояс then btn 470b,Привязать лодку
end

:470a
pln Ты отталкиваешься ногой и вагонетка, с лежащей в ней лодкой, начинает медленно разгоняться катясь под уклон.
pln На полной скорости вагонетка подпрыгивает над пропастью, и плохо закрепленная в ней лодка вылетает наружу.
p Из-за этого траектория движения вагонетки меняется и она, не долетев до противоположной стороны пропасти, падает вместе с тобой вниз, вслед за лодкой.
end

:470b
p Ты крепко привязываешь лодку к вагонетке кожанным ремнем.
inv- Кожанный ремень
inv- Лодка
btn 49b,Разогнать вагонетку
end

:48a
pln Высеченные в камне руны написаны на древнем языке гномов, но кое-где видны и полустёршиеся надписи сделанные хоббиттами.
p Тебе удаётся разобрать только слова: "заб..шен.ая шахт.", "гиг.н.ский	демон пещ..ы", ".ы...ая река в..ёт в ..ли.."
btn 48,Вернуться
end

:48c
pln Ты разгоняешь вагонетку и запрыгиваешь в неё на ходу. Рельсы идут под уклон и она разгоняется все сильней и сильней, когда внезапно одно из колес соскакивает с оси.
p На полной скорости вагонетка переворачивается, подминая тебя под себя. Сквозь скрежет металла слышится громкий хруст твоих костей.
end

:48d
pln Ты идешь по рельсам вглубь туннеля. Сначала дорога идёт под уклон, но потом рельсы внезапно обрываются и ты видишь перед собой глубокую, но не очень широкую пропасть.
p Подняв факел повыше, тебе удаётся разглядеть, что рельсы вновь появляются на другой стороне пропасти.
btn 48,Вернуться
btn 480,Перепрыгнуть пропасть
end

:480
pln Ты разбегаешься и прыгаешь через пропасть. В темноте она казалась тебе не такой широкой, какой оказалась на самом деле.
p Не допрыгнув до другой стороны ты падаешь вниз. Твой крик постепенно затихает в бездонной глубине пропасти.
end

:48b
pln Перед тобой старая ржавая вагонетка. На её дне видны остатки горной породы. Наверное, в далёкие времена гномы перевозили в ней много руды, потому что вагонетка довольно вместительная.
p Ты внимательно оглядываешь её со всех сторон и пробуешь сдвинуть с места. Ты замечаешь, что ось, крепящая одно из колес, плохо держится и почти с него соскочила.
btn 48,Вернуться
btn 49,Починить вагонетку
end

:49
pln Ты решаешь починить вагонетку...
if not Молоток then p ...но у тебя нет инструментов. & btn 48,Вернуться
if Молоток and not Вагонетка исправна then btn 48,Вернуться & btn 49a,Использовать молоток
if Вагонетка исправна then cls & p Ты уже починил вагонетку. & btn 48, Вернуться
end

:49a
p Ты стучишь молотком по колесу, насаживая его на ось. Теперь вагонетка исправна.
inv+ Вагонетка исправна
btn 48,Вернуться
btn 50t,Разогнать вагонетку
end

:50t
if Лодка then p Ты кладешь лодку рядом с вагонеткой. & inv- Лодка & inv+ Оставил лодку
pln Ты разгоняешь вагонетку и запрыгиваешь в неё на ходу. Рельсы идут под уклон и она разгоняется все сильней и сильней.
p На полной скорости вагонетка перепрыгивает через пропасть и приземляется точно на рельсы, на противоположной стороне бездны, замедляет ход и останавливается.
btn 50,Вылезти из вагонетки
end

:49b
pln Ты разгоняешь вагонетку и запрыгиваешь в неё на ходу. Рельсы идут под уклон и она разгоняется все сильней и сильней.
p На полной скорости вагонетка перепрыгивает через пропасть и приземляется точно на рельсы, на противоположной стороне бездны, замедляет ход и останавливается.
btn 500,Взять лодку
end

:500
p Ты берешь лодку из вагонетки. Теперь вагонетка пуста.
inv+ Лодка
btn 50,Осмотреться вокруг
end

:50
pln Ты стоишь на берегу подземной реки. Её воды бурным потоком вырываются из широкой трещины в стене пещеры и уносятся вдаль по туннелю.
p Вода в реке чистая и прозрачная. Сквозь неё ты видишь растущие на дне бурые водоросли.
btn 50a,Разогнать вагонетку
btn 50b,Вернуться к пропасти
btn 50c,Плыть по реке
if Лодка then btn 51,Использовать лодку
end

:50a
pln Ты разгоняешь вагонетку и запрыгиваешь в неё на ходу. Рельсы идут под уклон и она разгоняется все сильней и сильней.
pln На полной скорости вагонетка перепрыгивает через пропасть и приземляется точно на рельсы, на противоположной стороне бездны, замедляет ход и останавливается.
btn 48,Осмотреться вокруг
if Оставил лодку then p Ты берёшь оставленную тобой лодку. & inv+ Лодка & inv- Оставил лодку
end

:50b
pln Ты идешь по рельсам назад к пропасти. Сначала дорога идёт под уклон, но потом рельсы внезапно обрываются и ты видишь перед собой глубокую, но не очень широкую пропасть.
p Подняв факел повыше, тебе удаётся разглядеть, что рельсы вновь появляются на другой её стороне.
btn 50,Вернуться
btn 480,Перепрыгнуть пропасть
end

:50c
pln Ты ныряешь в прозрачную воду подземной реки. Течение подхватывает тебя и несёт вглубь темного туннеля.
p Внезапно из-под воды появляется чёрное щупальце, обхватывает твои ноги и утягивает тебя под воду.
end

:51
pln Ты спускаешь легкую эльфийскую лодку на воду. Загрузив в нее все свои вещи, ты отталкиваешься от берега ногой и пускаешься на поиски новых приключений.
inv- Лодка
pln
pln ***** ПРОДОЛЖЕНИЕ СЛЕДУЕТ *****
btn end,Заказать продолжение
btn end,Послесловие
btn end,Задать вопрос
end

:end
pln Примите мои поздравления. Вы только что прошли первую часть квеста "Древний кинжал".
pln
pln Хотите знать, что стало с хоббитом Фредом дальше?
pln Хотите бесплатно получить продолжение?
pln У вас есть вопросы и предложения?
pln Вы сделали собственный квест?
pln Вам понравился мой квест?
pln Тогда посетите наш сайт: "Текстовый квест" (http://textquest.nm.ru). Там вы всегда найдете самые свежие квесты для различных платформ, интересные материалы, ответы на все ваши вопросы, солюшены и т.д.
pln Или напишите мне на e-mail: textquest@nm.ru
pln Для меня очень важно ваше мнение. Не стесняйтесь указывать мне на ошибки и недочёты. Если я получу от вас много отзывов, то это сподвигнет меня на написание новых интересных квестов. Так что все зависит только от вас.
pln
pln До новых встреч. Спасибо за игру.
p Кащей.
btn 1,Главное меню
end